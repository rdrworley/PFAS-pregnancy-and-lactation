PROGRAM LactPFAS_Verner_2014

! Units: ug, L, h

INITIAL

! Subject-specific characteristics
constant AGE_delivery          = 25      ! Maternal age at delivery (years)
constant Weight_M_Prepregnancy = 70      ! Pre-pregnancy body weight (kg)
constant Birthweight           = 3.4     ! Birth weight (kg)
constant Weight_C              = 12.1    ! Child's weight (kg) at Age_child_weight
constant Age_weight_C          = 2.0     ! At age child weight measurement (years)
constant SEX                   = 0       ! Sex of infant (Female=0; Male=1)
constant TOTAL_BF              = 0.5     ! Total duration of breastfeeding (y)
constant AGE_CHILD_SAMP        = 2.0     ! Child age at blood sample (years)
constant DOSE_M                = 0.001   ! Maternal oral dose (ug/kg/d)
constant MAT_AGE_SAMPLE        = 29.8    ! Age at maternal blood sample (years)
constant COMPOUND              = 2       ! Compound (1:PFOS, 2:PFOA, 3:PFHxS)

! Calculate certain subject-specific characteristics
Age_conception    = AGE_delivery-0.768

! Weight profiles
table table_bw_females,1,10/0.0, 0.5, 1.0, 2.0, 5.0, 10.0, 13.0, 16.0, 20.0, 100.0, &
                            3.4, 7.2, 9.5, 12.1, 18.0, 33.0, 46.0, 54.0, 58.0, 58.0/
table table_bw_males  ,1,10/0.0, 0.5, 1.0, 2.0, 5.0, 10.0, 13.0, 16.0, 20.0, 100.0, &
                            3.5, 7.8, 10.3, 12.7, 18.0, 32.0, 46.0, 61.0, 71.0, 71.0/
table table_fetal_weight,1,7/0.0, 0.217, 0.353, 0.463, 0.551, 0.639, 0.768, &
                             0.0, 0.0, 0.26, 0.69, 1.25, 2.02, 3.28/

! Compound
if(compound .eq. 1) then
PFOS  = 1 
PFOA  = 0 
PFHXS = 0
else if(compound .eq. 2) then
PFOS  = 0 
PFOA  = 1 
PFHXS = 0
else if(compound .eq. 3) then
PFOS  = 0 
PFOA  = 0 
PFHXS = 1
endif

! Volume of distribution 
constant PFOS_VD_BW  = 0.230       ! PFOS (Thompson et al. 2010)
constant PFOA_VD_BW  = 0.170       ! PFOA (Thompson et al. 2010)
constant PFHXS_VD_BW = 0.213       ! PFHxS (monkeys: Sundstrom et al. 2012)
VD_BW = PFOS*PFOS_VD_BW+PFOA*PFOA_VD_BW+PFHXS*PFHXS_VD_BW

! Plasma:milk partition 
constant PFOS_PMilk  = 0.0138       ! PFOS (Fromme et al. 2010; Haug et al. 2011; Karrman et al. 2007; Kim et al. 2011; Liu et al. 2011)
constant PFOA_PMilk  = 0.0577       ! PFOA (Haug et al. 2011; Kim et al. 2011; Liu et al. 2011)
constant PFHXS_PMilk = 0.0140       ! PFHxS (Karrman et al. 2007; Kim et al. 2011)
PMilk = PFOS*PFOS_PMilk+PFOA*PFOA_PMilk+PFHXS*PFHXS_PMilk

! Cord:maternal plasma partition (Average of studies of paired samples in Aylward et al. 2014)
constant PFOS_P_CM  = 0.454         ! PFOS 
constant PFOA_P_CM  = 0.783         ! PFOA
constant PFHXS_P_CM = 0.556         ! PFHxS
P_CM = PFOS*PFOS_P_CM+PFOA*PFOA_P_CM+PFHXS*PFHXS_P_CM

! Half-lives (Olsen et al. )
constant PFOS_HL  = 5.4     ! PFOS half-life (h)
constant PFOA_HL  = 3.8     ! PFOA half-life (h)
constant PFHXS_HL = 8.5     ! PFHxS half-life (h) 
HALF_LIFE = (PFOS*PFOS_HL+PFOA*PFOA_HL+PFHXS*PFHXS_HL)*365*24

! Simulation time
TSTOP = (Age_delivery+5.0)*365*24

END

DYNAMIC

ALGORITHM IALG = 2        ! Use Gear algorithm
MAXTERVAL MAXT = 1E+10    ! Maximum interval for integrations
MINTERVAL MINT = 1E-10    ! Minimal interval for integrations
CINTERVAL CINT = 24       ! Communication interval

DERIVATIVE

! Maternal and child age (years)
AGE_M = t/(365*24)                           ! Age of mother (years)
AGE_C = IO_DELIVERY*(AGE_M-AGE_delivery)     ! Age of child (years)
GA    = IO_CONCEPTION*(AGE_M-AGE_conception) ! Gestational age (years)

! Maternal weight (kg)
BW_M = (Weight_M_Prepregnancy/table_bw_females(Age_conception))*table_bw_females(AGE_M)

! Child weight (kg)
initial
constant Multiplier_weight_c = 1.0
end
Adj_BW_C_pre  = Birthweight/3.28                                                                       ! Adjustment factor for prenatal growth
Adj_BW_C_post = Weight_C/((1-SEX)*table_bw_females(Age_weight_C)+SEX*table_bw_males(Age_weight_C))     ! Adjustment factor for postnatal growth
Adj_BW = (IO_CONCEPTION-IO_DELIVERY)*Adj_BW_C_pre + &
         (IO_DELIVERY-IO_WEIGHT_C)*(Adj_BW_C_pre+AGE_C*(Adj_BW_C_post-Adj_BW_C_pre)/Age_weight_C) + &
         (IO_WEIGHT_C)*Adj_BW_C_post                                                                   ! Adjustment factor for growth

BW_C = Multiplier_weight_c*Adj_BW*((IO_CONCEPTION-IO_DELIVERY)*table_fetal_weight(GA) + &
       (IO_DELIVERY)*((1-SEX)*table_bw_females(AGE_C)+SEX*table_bw_males(AGE_C)))

! Volume of distribution
VD_M = VD_BW*BW_M
VD_C = VD_BW*BW_C+0.0001

! Placental diffusion
DIFF_M_C = (IO_CONCEPTION-IO_DELIVERY)*C_MOTHER*P_CM
DIFF_C_M = (IO_CONCEPTION-IO_DELIVERY)*C_CHILD

! Breast milk transfer
initial
constant Multiplier_VMILK = 1.0
end
VOLUME_MILK_1ST_YEAR = BW_C*(-0.312*(AGE_C*365.0)+157.7)/24000.0
VOLUME_MILK_2ND_YEAR = (-0.2785*AGE_C+0.7203)/24 
VOLUME_MILK = IO_BF*Multiplier_VMILK*(IO_DELIVERY-IO_END_TOTAL_BF)* & 
((1-IO_CHILD_12m)*VOLUME_MILK_1ST_YEAR &    
+(IO_CHILD_12m-IO_CHILD_30m)*VOLUME_MILK_2ND_YEAR)

C_MILK      = Pmilk*C_MOTHER        ! Concentration in breast milk (ug/l)
TRANSFER_BF = VOLUME_MILK*C_MILK    ! Mother-child lactational transfer (ug)

! Elimination
ELIMINATION_M = C_MOTHER*VD_M*log(2)/HALF_LIFE
ELIMINATION_C = C_CHILD*VD_C*log(2)/HALF_LIFE

! Dosing
INTAKE_M = DOSE_M*BW_M/24
INTAKE_C = IO_CHILD_6m*DOSE_M*BW_C/24

! Switches I/O for time events
initial
IO_CONCEPTION          = 0
IO_DELIVERY            = 0
IO_CHILD_6m            = 0
IO_CHILD_12m           = 0
IO_CHILD_24m           = 0
IO_CHILD_30m           = 0
IO_WEIGHT_C            = 0
IO_END_TOTAL_BF        = 0
MOM_C_DEL              = 0
RATIO_CHILD_MOM_6M     = 0
C_CHILD_SAMP           = 0
C_CHILD_SAMP2          = 0
MOM_C_SAMP             = 0
AUCPRENATAL            = 0
AUC6MO                 = 0
AUC12MO                = 0
AUC24MO                = 0
AUC36MO                = 0         
end

if(TOTAL_BF .eq. 0) then 
IO_BF = 0
else if(TOTAL_BF .gt. 0) then 
IO_BF =1
endif

schedule conception .xn. (AGE_conception-AGE_M)
discrete conception
	IO_CONCEPTION = 1
End
schedule del .xn. (AGE_delivery-AGE_M)
discrete del
	IO_DELIVERY = 1
  MOM_C_DEL   = C_MOTHER
  AUCPRENATAL = AUC_C_CHILD
End
schedule momsamp .xn. (MAT_AGE_SAMPLE-AGE_M)
discrete momsamp
	MOM_C_SAMP = C_MOTHER
End
schedule child6m .xn. (0.500-AGE_C)
discrete child6m
  AUC6MO             = AUC_C_CHILD
  IO_CHILD_6m        = 1
  RATIO_CHILD_MOM_6M = C_CHILD/MOM_C_DEL
End
schedule child12m .xn. (1.000-AGE_C)
discrete child12m
	IO_CHILD_12m = 1
  AUC12MO      = AUC_C_CHILD
End
schedule child24m .xn. (2.000-AGE_C)
discrete child24m
	IO_CHILD_24m = 1
  AUC24MO      = AUC_C_CHILD
End
schedule child30m .xn. (2.500-AGE_C)
discrete child30m
	IO_CHILD_30m = 1
End
schedule child36m .xn. (3.000-AGE_C)
discrete child36m
  AUC36MO    = AUC_C_CHILD
End
schedule childw .xn. (Age_weight_C-AGE_C)
discrete childw
  IO_WEIGHT_C = 1
End
schedule endtotalbf .xn. (TOTAL_BF-AGE_C)
discrete endtotalbf
	IO_END_TOTAL_BF = 1
End
schedule childsamp1 .xn. (AGE_CHILD_SAMP-AGE_C)
discrete childsamp1
	C_CHILD_SAMP = C_CHILD
End
schedule childsamp2 .xn. (1.58-AGE_C)
discrete childsamp2
	C_CHILD_SAMP2 = C_CHILD
End

AUC0_6MO   = AUC6MO-AUCPRENATAL
AUC6_12MO  = AUC12MO-AUC6MO
AUC12_24MO = AUC24MO-AUC12MO
AUC24_36MO = AUC36MO-AUC24MO

!***************************************************************************
! Mass-balance differential equations

! Maternal compartment
RA_MOTHER = INTAKE_M-ELIMINATION_M-TRANSFER_BF-DIFF_M_C+DIFF_C_M
A_MOTHER  = INTEG(RA_MOTHER, 0.0)
C_MOTHER  = A_MOTHER/VD_M

! Child compartment
RA_CHILD    = INTAKE_C+TRANSFER_BF-ELIMINATION_C+DIFF_M_C-DIFF_C_M
A_CHILD     = INTEG(RA_CHILD, 0.0)
C_CHILD     = A_CHILD/VD_C
AUC_C_CHILD = INTEG(C_CHILD, 0.0)

! Ratios
DOSE_RATIO  = ((TRANSFER_BF+INTAKE_C)/(BW_C+0.000000001))/(INTAKE_M/(BW_M+0.000000001))
!DOSE_RATIO  = ((TRANSFER_BF+INTAKE_C)/(BW_C+0.000000001))/(DOSE_M+0.000000001) 	!old version of equation
LEVEL_RATIO = IO_DELIVERY*C_CHILD/(MOM_C_DEL+0.000000001)

! Collect outputs
ARRAY TIMESCOLLECT(220)
initial
do l1 ii1 = 1,220
schedule dummy .at. TIMESCOLLECT(ii1)
l1: continue
end
discrete dummy
robin = 0
end

TERMT (T .GE. (TSTOP))

END ! DERIVATIVE
END ! DYNAMIC
END ! PROGRAM